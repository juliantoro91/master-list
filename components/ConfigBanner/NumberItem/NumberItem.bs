sub init()
    m.label = m.top.findNode("label")
    m.input = m.top.findNode("input")

    m.keyboard = createObject("roSGNode", "StandardKeyboardDialog")
    m.keyboard.buttons = ["OK", "Cancel"]
    m.keyboard.keyboardDomain = "numeric"

    m.top.observeField("focusedChild", "onFocusChange")
    m.keyboard.observeField("buttonSelected", "onKeyboardAction")
    m.keyboard.observeField("wasClosed", "onKeyboardAction")

    m.navigation = {
        press : false
        key : ""
        timer : {
            timer : CreateObject("roSGNode", "Timer")
            initValue : 0.3
            currValue : 0
            minValue : 0.05
            modulator : 0.8
        }
    }
    m.navigation.timer.timer.observeField("fire", "navigateAction")
end sub

sub onFocusChange()
    if m.top.hasFocus()
        m.label.text = " > " + m.top.settings.label
        m.label.font.size = 32
        m.label.translation = [10, 30]
    else
        m.label.text = m.top.settings.label
        m.label.font.size = 24
        m.label.translation = [40, 30]
    end if
end sub

sub configureComponent(event as object)
    settings = event.getData()

    m.top.update(settings, true)

    m.label.text = settings.label
    m.input.text = getString(settings.value)

    m.keyboard.title = settings.label
    m.keyboard.text = m.input.text
end sub

sub onKeyEvent(key as string, press as boolean) as boolean
    m.navigation.press = press
    m.navigation.key = key

    if not press
        m.navigation.timer.currValue = m.navigation.timer.initValue
        return false
    end if

    if key = "OK"
        m.top.getScene().dialog = m.keyboard
        return true
    else if key = "right" or key = "left"
        navigateAction()
        return true
    end if

    return false
end sub

sub navigateAction()
    if m.navigation.press = false then return

    rate = 1
    if m.top.settings.subtype = "float" then rate = 0.1

    if m.navigation.key = "left" then rate = -rate

    m.top.value += rate

    m.input.text = getString(m.top.value)

    m.navigation.timer.currValue *= m.navigation.timer.modulator
    if m.navigation.timer.currValue < m.navigation.timer.minValue
        m.navigation.timer.currValue = m.navigation.timer.minValue
    end if
    timer = m.navigation.timer.timer
    timer.duration = m.navigation.timer.currValue
    timer.control = "start"
end sub

sub onKeyboardAction(event as object)
    selectedButton = event.getData()

    if selectedButton = 0
        m.top.value = m.keyboard.text
        m.input.text = getString(m.top.value)
    else if selectedButton = 1 or selectedButton = invalid
        m.keyboard.text = getString(m.top.value)
    end if

    m.keyboard.close = true
end sub

function getString(value as float)
    value = Cint(value * 10)

    return (value / 10).toStr()
end function
